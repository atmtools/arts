#DEFINITIONS:  -*-sh-*-
#
# Loads an atmosphere and basic absorption data. Absorption calculation is set
# to "on-the-fly".
#
# Planet          : Earth
# Frequency range : 0 - 100 GHz
# Dimensionality  : 3D
# Altitude range  : 0 - 1000 km, 250 m steps up to 80 km and 16 km above.
# Gas dataset     : Fascod tropical, expanded to 3D
# Gas species     : H20, O2 and N2, with standard absorption models
#
# Author: Patrick Eriksson

Arts2{

PlanetSet(option="Earth")
isotopologue_ratiosInitFromBuiltin

VectorNLogSpace( p_grid, 321, 1013e2, 1 )

VectorCreate( pionopshere )
VectorNLogSpace( pionopshere, 59, 0.1, 1e-59 )

Append( p_grid, pionopshere )

# Tempature and z covering the ionosphere
ReadXML( t_field_raw, "testdata/tropical.expanded.t.xml" )
ReadXML( z_field_raw, "testdata/tropical.expanded.z.xml" )

GriddedField3Create( raw3 )

abs_speciesInit

# N2
abs_speciesAdd( species = [ "N2-SelfContStandardType" ] )
ReadXML( raw3, "testdata/tropical.N2.xml"  )
Append( vmr_field_raw, raw3 )

# O2
abs_speciesAdd( species = [ "O2-PWR93" ] )
ReadXML( raw3, "testdata/tropical.O2.xml"  )
Append( vmr_field_raw, raw3 )

# H2O
abs_speciesAdd( species = [ "H2O-PWR98" ] )
ReadXML( raw3, "testdata/tropical.H2O.xml"  )
Append( vmr_field_raw, raw3 )


# Free electrons
abs_speciesAdd( species = [ "free_electrons" ] )
ReadXML( raw3, "testdata/ne_iri_solmax_spring_12UTC_0latlon.xml" )
Append( vmr_field_raw, raw3 )


# here, we do LTE only so far, but need to initialize NLTE t-field accordingly
Touch( nlte_field_raw )

# Interpolate to p_grid (VMR is "zero padded")
AtmFieldsCalcExpand1D( vmr_zeropadding=1 )

# Surface
Extract( z_surface, z_field, 0 )

# Apply HSE
Extract( p_hse, p_grid, 0 )
NumericSet( z_hse_accuracy, 0.1 )
atmfields_checkedCalc
z_fieldFromHSE

# Magnetic field, u-component
ReadXML( raw3, "planets/Earth/IGRF/IGRF11_2010_200km-5deg-5deg.B_u.xml.gz" )
GriddedFieldLatLonRegrid( raw3, lat_grid, lon_grid, raw3 )
GriddedFieldPRegrid( raw3, p_grid, raw3 )
FieldFromGriddedField( mag_u_field, p_grid, lat_grid, lon_grid, raw3 )

# Magnetic field, v-component
ReadXML( raw3, "planets/Earth/IGRF/IGRF11_2010_200km-5deg-5deg.B_v.xml.gz" )
GriddedFieldLatLonRegrid( raw3, lat_grid, lon_grid, raw3 )
GriddedFieldPRegrid( raw3, p_grid, raw3 )
FieldFromGriddedField( mag_v_field, p_grid, lat_grid, lon_grid, raw3 )

# Magnetic field, w-component
ReadXML( raw3, "planets/Earth/IGRF/IGRF11_2010_200km-5deg-5deg.B_w.xml.gz" )
GriddedFieldLatLonRegrid( raw3, lat_grid, lon_grid, raw3 )
GriddedFieldPRegrid( raw3, p_grid, raw3 )
FieldFromGriddedField( mag_w_field, p_grid, lat_grid, lon_grid, raw3 )

# No transitions here
abs_lines_per_speciesSetEmpty

# Absorption agendas
Copy( abs_xsec_agenda, abs_xsec_agenda__noCIA )

AgendaSet( propmat_clearsky_agenda ){
  Ignore(rtp_mag)
  Ignore(rtp_los)
  propmat_clearskyInit
  propmat_clearskyAddXsecAgenda
  propmat_clearskyAddFaraday
}

}
