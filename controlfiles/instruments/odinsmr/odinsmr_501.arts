# ARTS setup file for simulations of Odin-SMR measurements around 501.8 GHz.
# This band is part of the stratospheric mode, denoted as SM_AC2ab.
#
# The simulations are intended to match the operational settings, but there
# are some differences. See notes below.
#
# This file assumes that each tangent altitude is treated as a measurement
# block. 
#
# Input files are found in the folder tests/OdinSMR.
# See setup_input.m for source of input files.
#
# Author: Patrick Eriksson


Arts2 {


water_p_eq_agendaSet
gas_scattering_agendaSet
INCLUDE "general/planet_earth.arts"

INCLUDE "odinsmr_501_absorption.arts"


# Frequency grid
ReadXML( f_grid, "f_mono.SM_AC2ab.xml" )



#
# Sensor characteristics
# 

IndexSet( sensor_norm, 1 )


# Antenna:
#
IndexSet( antenna_dim, 1 )
#
VectorSet( za_grid, [
 -0.2,-0.15,-0.1,-0.05,-0.04,-0.03,-0.02,-0.01,
  0,0.01,0.02,0.03,0.04,0.05,0.10,0.15,0.20
] )
Matrix1ColFromVector( mblock_dlos_grid, za_grid )
#
MatrixSetConstant( antenna_dlos, 1, 1, 0 )

# The folder contains antenna pattern for different integration times.
# The pattern for smallest integration time is selected here.
ReadXML( antenna_response, "antenna.SM_AC2ab.875ms.xml" )


# Mixer:
#
ReadXML( lo, "lo.SM_AC2ab.xml" )
ReadXML( sideband_response, "sideband.SM_AC2ab.xml" )
StringSet( sideband_mode, "upper" )


# Spectrometer:
#
ReadXML( f_backend, "f_backend.SM_AC2ab.xml" )
ReadXML( backend_channel_response, "backend_channel_response.xml" )


sensor_responseInit
sensor_responseAntenna
sensor_responseMixer
sensor_responseIF2RF
sensor_responseBackend


} # End Arts
