#DEFINITIONS:  -*-sh-*-
#
############
# Agenda predefinitions for surface_rtprop_agenda
#
# This is a sub-file to agendas.arts, separated for better overview.
#
############
#
# Authors: Jana Mendrok
#


Arts2 {

##################################
#    surface_rtprop_agenda
##################################

#####
# blackbody surface
#####

# Fastest option, but only a good choice only if troposphere is opaque at the
# frequency in question.

# - preset (constant) surface skin temperature
# this is somewhat faster than the interpolation options
#
AgendaCreate( surface_rtprop_agenda__Blackbody_SurfTFix )
AgendaSet( surface_rtprop_agenda__Blackbody_SurfTFix ){
  Ignore( rtp_los )
  Ignore( rtp_pos )
  surfaceBlackbody
} 

# - surface skin temperature interpolated from atmospheric t_field
#
AgendaCreate( surface_rtprop_agenda__Blackbody_SurfTFromt_field )
AgendaSet( surface_rtprop_agenda__Blackbody_SurfTFromt_field ){
  Ignore( rtp_los )
  InterpAtmFieldToRtePos( out=surface_skin_t, pos=rtp_pos, field=t_field )
  surfaceBlackbody
} 


#####
# specular reflecting surfaces (no polarization)
#####

# - preset surface reflectivity (Vector)
# - preset (constant) surface skin temperature
# this is somewhat faster than the interpolation options
#
AgendaCreate( surface_rtprop_agenda__Specular_NoPol_ReflFix_SurfTFix )
AgendaSet( surface_rtprop_agenda__Specular_NoPol_ReflFix_SurfTFix ){
  specular_losCalc
  surfaceFlatScalarReflectivity
} 

# - preset surface reflectivity
# - surface skin temperature interpolated from t_surface
#
AgendaCreate( surface_rtprop_agenda__Specular_NoPol_ReflFix_SurfTFromt_surface )
AgendaSet( surface_rtprop_agenda__Specular_NoPol_ReflFix_SurfTFromt_surface ){
  specular_losCalc
  InterpSurfaceFieldToRtePos( out=surface_skin_t, pos=rtp_pos, field=t_surface )
  surfaceFlatScalarReflectivity
} 

# - preset surface reflectivity
# - surface skin temperature interpolated from atmospheric t_field
#
AgendaCreate( surface_rtprop_agenda__Specular_NoPol_ReflFix_SurfTFromt_field )
AgendaSet( surface_rtprop_agenda__Specular_NoPol_ReflFix_SurfTFromt_field ){
  specular_losCalc
  InterpAtmFieldToRtePos( out=surface_skin_t, pos=rtp_pos, field=t_field )
  surfaceFlatScalarReflectivity
}

# - surface reflectivity interpolated from surface reflectivity field (GriddedField4)
# - preset (constant) surface skin temperature
#
#AgendaCreate( surface_rtprop_agenda__Specular_NoPol_ReflFromField_SurfTFix )
#AgendaSet( surface_rtprop_agenda__Specular_NoPol_ReflFromField_SurfTFix ){
#  specular_losCalc
#  surface_scalar_reflectivityFromGriddedField4( r_field=r_gfield4 )
#  surfaceFlatScalarReflectivity
#} 

# - surface reflectivity from Liebe model
# - surface skin temperature interpolated from atmospheric t_field
#
AgendaCreate( surface_rtprop_agenda__Specular_Pol_Water_SurfTFromt_field )
AgendaSet( surface_rtprop_agenda__Specular_Pol_Water_SurfTFromt_field ){
  specular_losCalc
  InterpAtmFieldToRtePos( out=surface_skin_t, pos=rtp_pos, field=t_field )
  complex_refr_indexWaterLiebe93( complex_refr_index=surface_complex_refr_index,
                                  t=surface_skin_t )
  surfaceFlatRefractiveIndex
}


#####
# specular reflecting surfaces (considering polarization)
#####

# - preset surface reflectivity (Tensor3)
# - preset (constant) surface skin temperature
#
AgendaCreate( surface_rtprop_agenda__Specular_Pol_ReflFix_SurfTFix )
AgendaSet( surface_rtprop_agenda__Specular_Pol_ReflFix_SurfTFix ){
  specular_losCalc
  surfaceFlatReflectivity
} 

# - surface reflectivity interpolated from surface reflectivity field (GriddedField6)
# - preset (constant) surface skin temperature
#
#AgendaCreate( surface_rtprop_agenda__Specular_Pol_ReflFromField_SurfTFix )
#AgendaSet( surface_rtprop_agenda__Specular_Pol_ReflFromField_SurfTFix ){
#  specular_losCalc
#  surface_reflectivityFromGriddedField6( r_field=r_gfield6 )
#  surfaceFlatReflectivity
#} 

# - preset surface refractive index (Matrix)
# - preset (constant) surface skin temperature
#
AgendaCreate( surface_rtprop_agenda__Specular_Pol_RefracFix_SurfTFix )
AgendaSet( surface_rtprop_agenda__Specular_Pol_RefracFix_SurfTFix ){
  specular_losCalc
  surfaceFlatRefractiveIndex
} 

# - surface refractive index interpolated from surface refractive index field (GriddedField4)
# - preset (constant) surface skin temperature
#
#AgendaCreate( surface_rtprop_agenda__Specular_Pol_RefracFromField_SurfTFix )
#AgendaSet( surface_rtprop_agenda__Specular_Pol_RefracFromField_SurfTFix ){
#  specular_losCalc
#  surface_complex_refr_indexFromGriddedField4( complex_refr_index_field=r_gfield4 )
#  surfaceFlatRefractiveIndex
#} 


#####
# Lambertian reflecting surfaces (no polarization)
#####

# - preset surface reflectivity
# - preset (constant) surface skin temperature
# - preset number of angles, the scattering function will be evaluated on
#
AgendaCreate( surface_rtprop_agenda__lambertian_ReflFix_SurfTFix )
AgendaSet( surface_rtprop_agenda__lambertian_ReflFix_SurfTFix ){
  Ignore( rtp_pos )
  surfaceLambertianSimple( za_pos=0.5 )
} 

# - preset surface reflectivity
# - surface skin temperature interpolated from atmospheric t_field
# - user set number of angles, the scattering function will be evaluated on
#
AgendaCreate( surface_rtprop_agenda__lambertian_ReflFix_SurfTFromt_field )
AgendaSet( surface_rtprop_agenda__lambertian_ReflFix_SurfTFromt_field ){
  InterpAtmFieldToRtePos( out=surface_skin_t, pos=rtp_pos, field=t_field )
  surfaceLambertianSimple( za_pos=0.5 )
} 


}
