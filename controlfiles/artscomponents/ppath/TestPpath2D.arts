#DEFINITIONS:  -*-sh-*-
#
# Basic testing of 2D geometrical propagation paths
#
# Performs calculations with WGS84 ellipsoid

# Patrick Eriksson 2021-08-14

Arts2 {

# Define a 2D atmosphere
IndexSet( atmosphere_dim, 2 )
VectorLinSpace( z_grid, 0, 100e3, 1e3)
VectorLinSpace( lat_grid, 10, 45, 1)
VectorSet( lon_grid, [0] )
surface_elevationSetConstant(elevation=123)

# Ppath specific variables
NumericSet( ppath_lmax, 10e3 )
NumericSet( ppath_stop_distance, -1 )

# Set WGS84 
refellipsoidEarthZZZ( model = "WGS84" )

#
# No cloudbox
#
FlagOff( cloudbox_on )
ArrayOfIndexSet( cloudbox_limits, [] )

# From space missing the atmosphere
VectorSet( rte_pos, [600e3,11,22] )
VectorSet( rte_los, [100,9] )
ppathGeometric
ppathCheckStartPoint( background = 1,
                      np = 0,
                      zenith_angle = 100,
                      dzenith_angle = 1e-9,
                      azimuth_angle = 9,
                      dazimuth_angle = 1e-9 )

# From space looking into the surface
VectorSet( rte_pos, [600e3,34,-23] )
VectorSet( rte_los, [135,-39] )
ppathGeometric
ppathCheckStartPoint( background = 2,
                      altitude = 123,
                      daltitude = 1 )

# From space doing limb sounding
VectorSet( rte_pos, [600e3,55,3] )
VectorSet( rte_los, [113,-139] )
ppathGeometric
ppathCheckStartPoint( background = 1,
                      altitude = 100e3,
                      daltitude = 1,
                      zenith_angle = 82,  # Just a rough check that za is upward   
                      dzenith_angle = 3)

# From within the atmosphere looking up
VectorSet( rte_pos, [500,42,200] )
VectorSet( rte_los, [10,-89] )
ppathGeometric
ppathCheckStartPoint( background = 1,
                      altitude = 100e3,
                      daltitude = 1 )

# From within the atmosphere looking down towards the surface
VectorSet( rte_pos, [12e3,22.2,-180] )
VectorSet( rte_los, [135,39] )
ppathGeometric
ppathCheckStartPoint( background = 2,
                      altitude = 123,
                      daltitude = 1e-3 )

# Standing on the surface, looking down
VectorSet( rte_pos, [123,10,20] )
VectorSet( rte_los, [135,39] )
ppathGeometric
ppathCheckStartPoint( background = 2,
                      altitude = 123,
                      daltitude = 1e-3 )

# From within the atmosphere doing limb sounding
VectorSet( rte_pos, [20e3,36.23,0] )
VectorSet( rte_los, [93,-178] )
ppathGeometric
ppathCheckStartPoint( background = 1,
                      altitude = 100e3,
                      daltitude = 1,
                      zenith_angle = 81,    # Just a rough check that za is upward   
                      dzenith_angle = 3)

#
# With cloudbox
#
FlagOn( cloudbox_on )
ArrayOfIndexSet( cloudbox_limits, [10,20,10,20] )

# From space looking down on cloudbox
VectorSet( rte_pos, [600e3,23,23] )
VectorSet( rte_los, [180,-123] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      altitude = 20e3,
                      daltitude = 1 )

# From ground looking up on cloudbox
VectorSet( rte_pos, [123,27,-2] )
VectorSet( rte_los, [18,22] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      altitude = 10e3,
                      daltitude = 1 )

# Looking into south face of cloudbox
VectorSet( rte_pos, [19.5e3,19.05,-2] )
VectorSet( rte_los, [95,22] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      latitude = 20,
                      dlatitude = 0.01 )

# Looking into north face of cloudbox
VectorSet( rte_pos, [8.78e3,30.5,-180] )
VectorSet( rte_los, [81,-175] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      latitude = 30,
                      dlatitude = 0.01 )

# Some further tests where the intersection with the cloudbox is around corners
# That is, only zenith angle is changed to m ove from one face to next
# (while the tests above or more looking staright into a face)
VectorSet( rte_pos, [21e3,19.94,3] )
VectorSet( rte_los, [92,3] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      altitude = 20e3,
                      daltitude = 1 )
VectorSet( rte_los, [130,3] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      latitude = 20,
                      dlatitude = 0.01 )

VectorSet( rte_pos, [21e3,30.06,3] )
VectorSet( rte_los, [92,177] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      altitude = 20e3,
                      daltitude = 1 )
VectorSet( rte_los, [130,177] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      latitude = 30,
                      dlatitude = 0.01 )




# From within the cloudbox
VectorSet( rte_pos, [11e3,27,0] )
VectorSet( rte_los, [90,0] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      np = 1,
                      altitude = 11e3,
                      daltitude = 1,
                      zenith_angle = 90,    
                      dzenith_angle = 1e-5)

#
# With cloudbox reaching TOA
#
FlagOn( cloudbox_on )
ArrayOfIndexSet( cloudbox_limits, [10,100,10,20] )

# From space looking down on cloudbox
VectorSet( rte_pos, [600e3,29,23] )
VectorSet( rte_los, [153,-123] )
ppathGeometric
ppathCheckStartPoint( background = 3,
                      np = 1,
                      altitude = 100e3,
                      daltitude = 1 )
}