#DEFINITIONS:  -*-sh-*-
#
# As TestScatSolvers.arts but does only inclue DISORT and RT4, as well as a
#single frequency, so it can be included as a fast test.
#
Arts2 {


#
water_p_eq_agendaSet
gas_scattering_agendaSet
PlanetSet(option="Earth")

# Blackbody surface
surface_rtprop_agendaSet( option="Blackbody_SurfTFromt_field" )
VectorSet( surface_scalar_reflectivity, [0] )

# Standard ppath calculations
ppath_step_agendaSet( option="GeometricPath" )
ppath_agendaSet( option="FollowSensorLosPath" )

# Radiative transfer agendas
iy_main_agendaSet( option="Emission" )
iy_space_agendaSet( option="CosmicBackground" )
iy_surface_agendaSet( option="UseSurfaceRtprop" )
iy_cloudbox_agendaSet( option="QuarticInterpField" )

# Absorption species
abs_speciesSet( species=[ "N2-SelfContStandardType",
                          "O2-PWR98",
                          "H2O-PWR98"                          
                        ] )

# No line data needed here
abs_lines_per_speciesSetEmpty

propmat_clearsky_agendaAuto

# Dimensionality of the atmosphere
AtmosphereSet1D

# Brigtness temperatures used
StringSet( iy_unit, "PlanckBT" )

# Various things not used
ArrayOfStringSet( iy_aux_vars, [] )
jacobianOff

# Read data created by setup_test.m
ReadXML( p_grid,                  "testdata/p_grid.xml" )
ReadXML( t_field,                 "testdata/t_field.xml" )
ReadXML( z_field,                 "testdata/z_field.xml" )
ReadXML( vmr_field,               "testdata/vmr_field.xml" )
ReadXML( particle_bulkprop_field, "testdata/particle_bulkprop_field" )
ReadXML( particle_bulkprop_names, "testdata/particle_bulkprop_names" )
ReadXML( scat_data_raw,           "testdata/scat_data.xml" )
ReadXML( scat_meta,               "testdata/scat_meta.xml" )

# Define hydrometeors
#
StringCreate( species_id_string )
#
# Scat species 0
StringSet( species_id_string, "RWC" )
ArrayOfStringSet( pnd_agenda_input_names, [ "RWC" ] )
ArrayOfAgendaAppend( pnd_agenda_array ){
  ScatSpeciesSizeMassInfo( species_index=agenda_array_index, x_unit="dveq" )
  Copy( psd_size_grid, scat_species_x )
  Copy( pnd_size_grid, scat_species_x )
  psdWangEtAl16( t_min = 273, t_max = 999 )
  pndFromPsdBasic
}
Append( scat_species, species_id_string )
Append( pnd_agenda_array_input_names, pnd_agenda_input_names )
#
# Scat species 1
StringSet( species_id_string, "IWC" )
ArrayOfStringSet( pnd_agenda_input_names, [ "IWC" ] )
ArrayOfAgendaAppend( pnd_agenda_array ){
  ScatSpeciesSizeMassInfo( species_index=agenda_array_index, x_unit="dveq",
                           x_fit_start=100e-6 )
  Copy( psd_size_grid, scat_species_x )
  Copy( pnd_size_grid, scat_species_x )
  psdMcFarquaharHeymsfield97( t_min = 10, t_max = 273, t_min_psd = 210 )
  pndFromPsdBasic
}
Append( scat_species, species_id_string )
Append( pnd_agenda_array_input_names, pnd_agenda_input_names )



# Special settings for the scattering solvers
#
# Angular grid DISORT
DOAngularGridsSet( N_za_grid=38 )
# RT4 creates own grirs, so we need copies of the ones created above
VectorCreate( za_grid_copy )
VectorCreate( aa_grid_copy )
#
# Hybrid requires that ppath_lmax is not too high
NumericSet( ppath_lmax, 100 )
AgendaCreate( iy_hybrid_agenda )
AgendaSet( iy_hybrid_agenda ){
  Ignore( iy_id )
  ppathCalc( cloudbox_on = 0 ) 
  iyEmissionHybrid
  # The line below is just temporary
  Touch( iy_aux)
  VectorSet(geo_pos, [])
}

# Versions of y for various calculations
VectorCreate( y_disort )
VectorCreate( y_rt4 )
VectorCreate( y_hybrid )
#
StringCreate( message )

# Perform some basic checks
lbl_checkedCalc
atmfields_checkedCalc


# Intitial settings for tests
IndexSet( stokes_dim, 1 )
# See non-fast cfile for possible frequencies to select
VectorSet( f_grid, [165e9] )
Extract( z_surface, z_field, 0 )
MatrixSet( sensor_pos, [20e3;8e3;20e3] )
MatrixSet( sensor_los, [180;160;130] )

# Some stuff that depends on the settings above
sensorOff
atmgeom_checkedCalc
sensor_checkedCalc
scat_dataCalc
scat_data_checkedCalc
#
VectorExtractFromMatrix( rtp_pos, z_surface, 0, "row" )
InterpAtmFieldToPosition( out=surface_skin_t, field=t_field )


# Test 1: Cloubox with no scattering
# ---------------------------------------------------------------------
cloudboxSetFullAtm
pnd_fieldZero
cloudbox_checkedCalc
#
StringSet( message, "No scattering" )
Print( message, 0 )
#
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
INCLUDE "run_hybrid.arts"
Print( y_hybrid, 0 )
#
Compare( y_rt4, y_hybrid, 0.02, "Zero particles, RT4" )
Compare( y_disort, y_hybrid, 0.02, "Zero particles, DISORT" )


# Test 2: With nominal RWC/IWC and surface at p_grid[0]
# ---------------------------------------------------------------------
pnd_fieldCalcFromParticleBulkProps
cloudbox_checkedCalc
#
StringSet( message, "Nominal case" )
Print( message, 0 )
#
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
#
Compare( y_rt4, y_disort, 0.2, "Test2, RT4 vs. DISORT" )


# Test 3: As 2 but with RWC/IWC increased with a factor of 3
# ---------------------------------------------------------------------
Tensor4Multiply( particle_bulkprop_field, particle_bulkprop_field, 3 )
pnd_fieldCalcFromParticleBulkProps
#
StringSet( message, "Increased RWC/IWC" )
Print( message, 0 )
#
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
#
Compare( y_rt4, y_disort, 0.2, "Test3, RT4 vs. DISORT" )


# Test 4: As 2 but with surface moved upwards
# ---------------------------------------------------------------------
Tensor4Multiply( particle_bulkprop_field, particle_bulkprop_field, 0.3333333 )
pnd_fieldCalcFromParticleBulkProps
MatrixAdd( z_surface, z_surface, 3000 )
VectorExtractFromMatrix( rtp_pos, z_surface, 0, "row" )
InterpAtmFieldToPosition( out=surface_skin_t, field=t_field )
##
StringSet( message, "Surface moved upwards" )
Print( message, 0 )
#
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
#
Compare( y_rt4, y_disort, 0.2, "Test4, RT4 vs. DISORT" )

}
 
