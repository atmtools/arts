#DEFINITIONS:  -*-sh-*-
#
# To be written ...
#
Arts2 {


INCLUDE "general/continua.arts"
INCLUDE "general/agendas.arts"
INCLUDE "general/planet_earth.arts"

# Blackbody surface
Copy( surface_rtprop_agenda, surface_rtprop_agenda__Blackbody_SurfTFromt_field )
VectorSet( surface_scalar_reflectivity, [0] )

# Standard ppath calculations
Copy( ppath_step_agenda, ppath_step_agenda__GeometricPath )
Copy( ppath_agenda, ppath_agenda__FollowSensorLosPath )

# Radiative transfer agendas
Copy( iy_main_agenda, iy_main_agenda__Emission )
Copy( iy_space_agenda, iy_space_agenda__CosmicBackground )
Copy( iy_surface_agenda, iy_surface_agenda__UseSurfaceRtprop )
Copy( iy_cloudbox_agenda,  iy_cloudbox_agenda__QuarticInterpField )

# Absorption species
abs_speciesSet( species=[ "N2-SelfContStandardType",
                          "O2-PWR93",
                          "H2O-PWR98"                          
                        ] )

# No line data needed here
abs_lines_per_speciesSetEmpty

propmat_clearsky_agendaSetAutomatic

# Dimensionality of the atmosphere
AtmosphereSet1D

# Brigtness temperatures used
StringSet( iy_unit, "PlanckBT" )

# Various things not used
ArrayOfStringSet( iy_aux_vars, [] )
jacobianOff

# Read data created by setup_test.m
ReadXML( p_grid,                  "testdata/p_grid.xml" )
ReadXML( t_field,                 "testdata/t_field.xml" )
ReadXML( z_field,                 "testdata/z_field.xml" )
ReadXML( vmr_field,               "testdata/vmr_field.xml" )
ReadXML( particle_bulkprop_field, "testdata/particle_bulkprop_field" )
ReadXML( particle_bulkprop_names, "testdata/particle_bulkprop_names" )
ReadXML( scat_data_raw,           "testdata/scat_data.xml" )
ReadXML( scat_meta,               "testdata/scat_meta.xml" )

# Define hydrometeors
#
StringCreate( species_id_string )
#
# Scat species 0
StringSet( species_id_string, "RWC" )
ArrayOfStringSet( pnd_agenda_input_names, [ "RWC" ] )
ArrayOfAgendaAppend( pnd_agenda_array ){
  ScatSpeciesSizeMassInfo( species_index=agenda_array_index, x_unit="dveq" )
  Copy( psd_size_grid, scat_species_x )
  Copy( pnd_size_grid, scat_species_x )
  psdWangEtAl16( t_min = 273, t_max = 999 )
  pndFromPsdBasic
}
Append( scat_species, species_id_string )
Append( pnd_agenda_array_input_names, pnd_agenda_input_names )
#
# Scat species 1
StringSet( species_id_string, "IWC" )
ArrayOfStringSet( pnd_agenda_input_names, [ "IWC" ] )
ArrayOfAgendaAppend( pnd_agenda_array ){
  ScatSpeciesSizeMassInfo( species_index=agenda_array_index, x_unit="dveq",
                           x_fit_start=100e-6 )
  Copy( psd_size_grid, scat_species_x )
  Copy( pnd_size_grid, scat_species_x )
  psdMcFarquaharHeymsfield97( t_min = 10, t_max = 273, t_min_psd = 210 )
  pndFromPsdBasic
}
Append( scat_species, species_id_string )
Append( pnd_agenda_array_input_names, pnd_agenda_input_names )



# Special settings for the scattering solvers
#
# Angular grids for DOIT and DISORT
DOAngularGridsSet( N_za_grid=38, N_aa_grid=37 )
#
# DOIT stuff
AgendaSet( pha_mat_spt_agenda ){ pha_mat_sptFromDataDOITOpt }
AgendaSet( spt_calc_agenda ){ opt_prop_sptFromMonoData }
AgendaSet( doit_scat_field_agenda ){ doit_scat_fieldCalc }

AgendaSet( doit_mono_agenda ){
  DoitScatteringDataPrepare
  Ignore( f_grid )
  cloudbox_field_monoIterate
}
AgendaSet( doit_rte_agenda ){
  cloudbox_fieldUpdateSeq1D( normalize=1,
                           norm_error_threshold=0.05 )
}
doit_za_interpSet( interp_method="linear" )
AgendaSet( doit_conv_test_agenda ){ doit_conv_flagAbsBT( epsilon=[0.1] ) }
#
# RT4 creates own grirs, so we need copies of the ones created above
VectorCreate( za_grid_copy )
VectorCreate( aa_grid_copy )
#
# Hybrid requires that ppath_lmax is not too high
NumericSet( ppath_lmax, 100 )
AgendaCreate( iy_hybrid_agenda )
AgendaSet( iy_hybrid_agenda ){
  Ignore( iy_id )
  ppathCalc( cloudbox_on = 0 ) 
  iyEmissionHybrid
  # The line below is just temporary
  Touch( iy_aux)
  VectorSet(geo_pos, [])
}

# Versions of y for various calculations
VectorCreate( y_doit )
VectorCreate( y_disort )
VectorCreate( y_hybrid )
VectorCreate( y_rt4 )
#
StringCreate( message )

# Perform some basic checks
lbl_checkedCalc
atmfields_checkedCalc


# Intitial settings for tests
IndexSet( stokes_dim, 1 )
# Scattering data tailored to these frequencies, so don't change!
VectorSet( f_grid, [31.5e9,165e9,666e9] )
Extract( z_surface, z_field, 0 )
MatrixSet( sensor_pos, [20e3;20e3;10e3;5e3] )
MatrixSet( sensor_los, [180;130;160;20] )

# Some stuff that depends on the settings above
sensorOff
atmgeom_checkedCalc
sensor_checkedCalc
scat_dataCalc
# We need here a higher sca_mat_threshold. Otherwise there is an error for RWC
# and 668 GHz
scat_data_checkedCalc( sca_mat_threshold=0.25 )
#
VectorExtractFromMatrix( rtp_pos, z_surface, 0, "row" )
InterpAtmFieldToPosition( out=surface_skin_t, field=t_field )


# Test 1: Cloubox with no scattering
# ---------------------------------------------------------------------
cloudboxSetFullAtm
pnd_fieldZero
cloudbox_checkedCalc
#
StringSet( message, "No scattering" )
Print( message, 0 )
#
INCLUDE "run_doit.arts"
Print( y_doit, 0 )
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
INCLUDE "run_hybrid.arts"
Print( y_hybrid, 0 )
#
Compare( y_doit, y_hybrid, 0.1, "Zero particles, DOIT" )
Compare( y_rt4, y_hybrid, 0.1, "Zero particles, RT4" )
Compare( y_disort, y_hybrid, 0.1, "Zero particles, DISORT" )


# Test 2: With nominal RWC/IWC and surface at p_grid[0]
# ---------------------------------------------------------------------
pnd_fieldCalcFromParticleBulkProps
cloudbox_checkedCalc
#
StringSet( message, "Nominal case" )
Print( message, 0 )
#
INCLUDE "run_doit.arts"
Print( y_doit, 0 )
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
INCLUDE "run_hybrid.arts"
Print( y_hybrid, 0 )
#
Compare( y_doit, y_disort, 0.4, "Test2, DOIT vs. DISORT" )
Compare( y_rt4, y_disort, 0.8, "Test2, RT4 vs. DISORT" )
Compare( y_hybrid, y_disort, 0.4, "Test2, Hybrid vs. DISORT" )


# Test 3: As 2 but with RWC/IWC increased with a factor of 3
# ---------------------------------------------------------------------
Tensor4Multiply( particle_bulkprop_field, particle_bulkprop_field, 3 )
pnd_fieldCalcFromParticleBulkProps
#
StringSet( message, "Increased RWC/IWC" )
Print( message, 0 )
#
INCLUDE "run_doit.arts"
Print( y_doit, 0 )
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
INCLUDE "run_hybrid.arts"
Print( y_hybrid, 0 )
#
Compare( y_doit, y_disort, 1.0, "Test3, DOIT vs. DISORT" )
Compare( y_rt4, y_disort, 0.8, "Test3, RT4 vs. DISORT" )
Compare( y_hybrid, y_disort, 0.4, "Test3, Hybrid vs. DISORT" )


# Test 4: As 2 but with surface moved upwards
# ---------------------------------------------------------------------
Tensor4Multiply( particle_bulkprop_field, particle_bulkprop_field, 0.3333333 )
pnd_fieldCalcFromParticleBulkProps
MatrixAdd( z_surface, z_surface, 3000 )
VectorExtractFromMatrix( rtp_pos, z_surface, 0, "row" )
InterpAtmFieldToPosition( out=surface_skin_t, field=t_field )
##
StringSet( message, "Surface moved upwards" )
Print( message, 0 )
#
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
INCLUDE "run_hybrid.arts"
Print( y_hybrid, 0 )
#
Compare( y_rt4, y_disort, 0.8, "Test4, RT4 vs. DISORT" )
Compare( y_hybrid, y_disort, 0.4, "Test4, Hybrid vs. DISORT" )

}
 
