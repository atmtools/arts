Arts2 {


INCLUDE "general/continua.arts"
water_p_eq_agendaSet
gas_scattering_agendaSet

AgendaSet(propmat_clearsky_agenda)
{
Ignore(rtp_mag)
Ignore(rtp_los)
propmat_clearskyInit
propmat_clearskyAddLines
}

iy_main_agendaSet(option="Emission")
iy_surface_agendaSet
iy_space_agendaSet
ppath_agendaSet( option="FollowSensorLosPath" )
ppath_step_agendaSet( option="GeometricPath" )
surface_rtprop_agendaSet( option="Blackbody_SurfTFromt_field" )

IndexSet( stokes_dim, 1 )

VectorNLogSpace( p_grid, 41, 1000e2, 1 )
VectorNLinSpace( lat_grid, 2, -60, 60)
VectorNLinSpace( lon_grid, 2, -30, 30)
MatrixSet(sensor_los, [1.126885514276187e2,0;   1.122589281936843e2,0;   1.118213323268050e2,0])
MatrixSet(sensor_pos, [600e3, 0, 0;600e3,0,0;600e3,0,0])

refellipsoidEarth( refellipsoid, "Sphere" )
AtmosphereSet3D

abs_speciesSet( species=["CO2-626"] )
 
VectorCreate(tmp_kayser)
VectorLinSpace( tmp_kayser, 600, 650, 1 )
FrequencyFromCGSKayserWavenumber( f_grid, tmp_kayser )

#WriteXML("ascii",tmp_kayser,"cm.xml")
#WriteXML("ascii",f_grid,"f.xml")

ReadArrayOfARTSCAT(abs_lines=abs_lines, filename="../../testdata/NLTE_CO2_testlines.xml", globalquantumnumbers="v1 v2 v3 r l2", localquantumnumbers="J")
abs_linesNormalization(option="VVH")
abs_linesCutoff(option="ByLine", value=750e9)
abs_lines_per_speciesCreateFromLines
isotopologue_ratiosInitFromBuiltin
sensorOff
jacobianOff
cloudboxOff
StringSet( iy_unit, "W/(m^2 m-1 sr)" )

# Note that this data is not at all reliable and does not represent a real world scenario...
AtmWithNLTERawRead(  basename = "testdata/tropical", expect_vibrational_energies=1  )
AtmFieldsCalcExpand1D
Extract( z_surface, z_field, 0 )

VectorCreate(ev)
ReadXML(ev,"testdata/tropical.ev.xml")
abs_lines_per_speciesPopulationNlteField

#NumericSet(ppath_lmax,5e3)
propmat_clearsky_agenda_checkedCalc
atmfields_checkedCalc
atmgeom_checkedCalc
cloudbox_checkedCalc
sensor_checkedCalc
lbl_checkedCalc
yCalc

VectorCreate(y_nlte_ref)
# WriteXML("ascii",y,"TestNLTE_NLTE_REFERENCE.xml")
ReadXML(y_nlte_ref,"TestNLTE_NLTE_REFERENCE.xml")
CompareRelative(y,y_nlte_ref,1e-5, "NLTE old method")

EnergyLevelMapCreate(tmp_elm)
ArrayOfQuantumIdentifierCreate(tmp_aoqi)
Copy(tmp_elm, nlte_field)
Copy(tmp_aoqi, nlte_level_identifiers)
nlteOff
abs_lines_per_speciesPopulation(option="LTE")

propmat_clearsky_agenda_checkedCalc
atmfields_checkedCalc
atmgeom_checkedCalc
cloudbox_checkedCalc
sensor_checkedCalc
yCalc

VectorCreate(y_lte_ref)
# WriteXML("ascii",y,"TestNLTE_LTE_REFERENCE.xml")
ReadXML(y_lte_ref, "TestNLTE_LTE_REFERENCE.xml")
CompareRelative(y,y_lte_ref,1e-5, "LTE old method")
}
