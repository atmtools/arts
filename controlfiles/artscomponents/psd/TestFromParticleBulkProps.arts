#DEFINITIONS:  -*-sh-*-

Arts2 {


INCLUDE "general/agendas.arts"
INCLUDE "general/planet_earth.arts"

# Define hydrometeors
#
ReadXML( scat_data_raw, "testdata/scat_data.xml" )
ReadXML( scat_meta,     "testdata/scat_meta.xml" )
#
StringCreate( species_id_string )
#
# Scat species 0
StringSet( species_id_string, "RWC" )
ArrayOfStringSet( pnd_agenda_input_names, [ "RWC", "Dmean" ] )
ArrayOfAgendaAppend( pnd_agenda_array ){
  ScatSpeciesSizeMassInfo( species_index=agenda_array_index, x_unit="dmax" )
  Copy( psd_size_grid, scat_species_x )
  Copy( pnd_size_grid, scat_species_x )
  psdModifiedGammaMassXmean(n0=-999, mu=2, la=-999, ga=1, t_min=200, t_max=300)
  pndFromPsdBasic
}
Append( scat_species, species_id_string )
Append( pnd_agenda_array_input_names, pnd_agenda_input_names )
#
# Scat species 1
StringSet( species_id_string, "IWC" )
ArrayOfStringSet( pnd_agenda_input_names, [ "IWC" ] )
ArrayOfAgendaAppend( pnd_agenda_array ){
  ScatSpeciesSizeMassInfo( species_index=agenda_array_index, x_unit="dveq",
                           x_fit_start=100e-6 )
  Copy( psd_size_grid, scat_species_x )
  Copy( pnd_size_grid, scat_species_x )
  psdMcFarquaharHeymsfield97( t_min = 10, t_max = 273, t_min_psd = 210 )
  pndFromPsdBasic
}
Append( scat_species, species_id_string )
Append( pnd_agenda_array_input_names, pnd_agenda_input_names )



# Variables for both 1D and 3D

IndexCreate( np )
IndexSet( np, 21 )
VectorNLogSpace( p_grid, np, 1000e2, 100e2 )

IndexCreate( ip_last )
IndexAdd( ip_last, np, -1 )

NumericCreate( tatm )
NumericSet( tatm, 270 )

VectorSet( f_grid, [150e9] )
scat_dataCalc

FlagOn( atmfields_checked )

jacobianOff

ArrayOfStringSet( particle_bulkprop_names, [ "RWC", "Dmean", "IWC" ] )



# 1D with zeros everywhere

AtmosphereSet1D
Tensor3SetConstant( t_field, np, 1, 1, tatm )

Tensor4SetConstant( particle_bulkprop_field, 3, np, 1, 1, 0 )

# Test with different sizes of cloud box
#
cloudboxSetManually( p1=700e2, p2=300e2, lat1=0, lat2=0, lon1=0, lon2=0 )
pnd_fieldCalcFromParticleBulkProps

#
cloudboxSetFullAtm
pnd_fieldCalcFromParticleBulkProps
#
cloudboxSetManually( p1=2000e2, p2=300e2, lat1=0, lat2=0, lon1=0, lon2=0 )
pnd_fieldCalcFromParticleBulkProps
#
cloudboxSetManually( p1=700e2, p2=1, lat1=0, lat2=0, lon1=0, lon2=0 )
pnd_fieldCalcFromParticleBulkProps



# 3D with zeros everywhere

IndexCreate( nlatlon )
IndexSet( nlatlon, 11 )
VectorNLinSpace( lat_grid, nlatlon, -10, 10 )
Copy( lon_grid, lat_grid ) 

AtmosphereSet3D
Tensor3SetConstant( t_field, np, nlatlon, nlatlon, tatm )

Tensor4SetConstant( particle_bulkprop_field, 3, np, nlatlon, nlatlon, 0 )

# Test with different sizes of cloud box
#
cloudboxSetManually( p1=700e2, p2=300e2, lat1=-5, lat2=5, lon1=-7, lon2=3 )
pnd_fieldCalcFromParticleBulkProps
#
cloudboxSetManually( p1=2000e2, p2=1, lat1=-8, lat2=8, lon1=-7, lon2=3 )
pnd_fieldCalcFromParticleBulkProps



# 1D with an actual bulkprop field

AtmosphereSet1D
Tensor3SetConstant( t_field, np, 1, 1, tatm )

# This file contains non-zero at index 5:13
ReadXML( particle_bulkprop_field,
         "testdata/particle_bulkprop_field1.xml" )

cloudboxSetFullAtm
pnd_fieldCalcFromParticleBulkProps



# And add a Jacobian part

jacobianInit
#
jacobianAddScatSpecies( 
    species  = "RWC",
    quantity = "Dmean",
    g1 = p_grid,
    g2 = lat_grid,
    g3 = lon_grid
)
jacobianAddScatSpecies( 
    species  = "IWC",
    quantity = "IWC",
    g1 = p_grid,
    g2 = lat_grid,
    g3 = lon_grid
)
jacobianClose

ArrayOfIndexSet( cloudbox_limits, [4,14] )
pnd_fieldCalcFromParticleBulkProps


# The case below should generate an error, activate to check that this happens
# and an accaptable error message is given
#cloudboxSetFullAtm
#pnd_fieldCalcFromParticleBulkProps

}
