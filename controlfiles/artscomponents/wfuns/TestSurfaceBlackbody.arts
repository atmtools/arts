#DEFINITIONS:  -*-sh-*-
#
# A demonstration of how to set up a calcuulation involving the Jacobian of a
# surface variable.
#
# SurfaceBlackbody is used for demonstration and the provided Jacobian also
# tested by a perturbation calculation.
#
# 2021-03-31, Patrick Eriksson

Arts2 {


LegacyContinuaInit
water_p_eq_agendaSet
gas_scattering_agendaSet
PlanetSet(option="Earth")
isotopologue_ratiosInitFromBuiltin

# (standard) emission calculation
iy_main_agendaSet(option="Emission")

# cosmic background radiation
iy_space_agendaSet

# sensor-only path
ppath_agendaSet( option="FollowSensorLosPath" )

# no refraction
ppath_step_agendaSet( option="GeometricPath" )

# Frequencies and Stokes
#
IndexSet( stokes_dim, 1 )
VectorLinSpace( f_grid, 10e9, 90e9, 10e9 )

# A pressure grid rougly matching 0 to 30 km
#
VectorNLogSpace( p_grid, 31, 1000e2, 100e2 )

# Definition of species
# 
abs_speciesSet( species= [ "H2O-PWR98", "O2-PWR98", "N2-SelfContStandardType" ] )

# No line data needed here
# 
abs_lines_per_speciesSetEmpty

propmat_clearsky_agendaAuto

# Dimensionality of the atmosphere
#
AtmosphereSet1D

# Atmospheric profiles
# 
AtmRawRead( basename = "testdata/tropical" )
#
AtmFieldsCalc

# Get ground altitude (z_surface) from z_field
Extract( z_surface, z_field, 0 )

# Definition of position and LOS (simulating limb sounding from 600 km)
#
MatrixSet( sensor_pos, [800e3] )
MatrixSet( sensor_los, [135] )

# Define auxiliary data (the order between the quantities is free)
#
ArrayOfStringSet( iy_aux_vars, [] )

# "surface_props"
#
ArrayOfStringSet( surface_props_names, [ "Skin temperature" ] )
Tensor3SetConstant( surface_props_data, 1, 1, 1, 275 )

# Surface agenda
#
AgendaSet( iy_surface_agenda ){
  SurfaceBlackbody
  iySurfaceRtpropCalc
}

# Define Jacobian
#
jacobianInit
jacobianAddSurfaceQuantity( g1 = lat_grid, g2 = lon_grid, 
    quantity = "Skin temperature" )
jacobianClose

# No scattering nor sensor
#
cloudboxOff
sensorOff

# Perform RT calculations
#
lbl_checkedCalc
atmfields_checkedCalc
atmgeom_checkedCalc
cloudbox_checkedCalc
sensor_checkedCalc
#
StringSet( iy_unit, "RJBT" )
#
yCalc


# To save calculated spectrum and transmission
#
#output_file_formatSetAscii
#WriteXML( output_file_format, y, "y.xml" )
#WriteXML( output_file_format, jacobian, "j.xml" )

# Copy main output
#
VectorCreate( y0 )
Copy( y0, y )
#
MatrixCreate( j0 )
Copy( j0, jacobian )


# Perturb  with 0.01K to check Jacobian
#
jacobianOff
Tensor3Add( surface_props_data, surface_props_data, 0.01 )
#
yCalc
VectorSubtractElementwise( y, y, y0 )
VectorMultiply( y, y, 100 )
#
VectorCreate( jvec )
VectorExtractFromMatrix( jvec, j0, 0, "column" )
#
Compare( y, jvec, 1e-6 )

}
 
