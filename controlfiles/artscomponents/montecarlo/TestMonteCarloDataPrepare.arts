#DEFINITIONS:  -*-sh-*-
#This control file prepares a lot of atmospheric field data for the ARTS-MC examples
#simpleMCGeneral.arts, and simpleMCGeneralGaussian.arts, simpleMC.arts


Arts2 {

INCLUDE "general/general.arts"
INCLUDE "general/continua.arts"
INCLUDE "general/agendas.arts"
INCLUDE "general/planet_earth.arts"

# Agenda for scalar gas absorption calculation
Copy(abs_xsec_agenda, abs_xsec_agenda__noCIA)

output_file_formatSetBinary

VectorSet( f_grid, [ 230e9 ] )

WriteXML( output_file_format, f_grid )

IndexSet( f_index, 0 )

IndexSet( stokes_dim, 4 )

AtmosphereSet3D

ReadXML( p_grid, "p_grid.xml" )

ReadXML( lat_grid, "lat_grid.xml" )

ReadXML( lon_grid, "lon_grid.xml" )

abs_speciesSet( species=
                [ "O2-PWR93", "N2-SelfContStandardType", "H2O-PWR98" ] )

abs_lines_per_speciesSetEmpty

AtmRawRead( basename="testdata/tropical" )

AtmFieldsCalcExpand1D

WriteXML( output_file_format, t_field )

WriteXML( output_file_format, z_field )

WriteXML( output_file_format, vmr_field )

nelemGet( nrows, lat_grid )

nelemGet( ncols, lon_grid )

MatrixSetConstant( z_surface, nrows, ncols, 500.0 )

WriteXML( output_file_format, z_surface )

abs_xsec_agenda_checkedCalc

atmfields_checkedCalc

jacobianOff

abs_lookupSetup
abs_lookupCalc
WriteXML( output_file_format, abs_lookup )


cloudboxSetManually( p1=21617.7922264, p2=17111.6808705,
                     lat1=-1.9, lat2=1.9, lon1=-1.9, lon2=1.9 )

Print( cloudbox_limits, 1 )

ScatSpeciesInit

ScatElementsPndAndScatAdd( 
  scat_data_files=["testdata/scatData/azi-random_f229-231T214-225r100NP-1ar1_5ice.xml"],
  pnd_field_files=[""]
)

ReadXML( pnd_field_raw, "pnd_field_raw.xml" )

pnd_fieldCalcFrompnd_field_raw

WriteXML( output_file_format, pnd_field )

scat_data_monoCalc

#For some reason SingleScatteringData binary files can't be loaded 
output_file_formatSetAscii

WriteXML( output_file_format, scat_data_raw )

WriteXML( output_file_format, scat_data_mono )

WriteXML( output_file_format, cloudbox_limits )

}
