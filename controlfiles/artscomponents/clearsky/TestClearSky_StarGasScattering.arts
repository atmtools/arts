#DEFINITIONS:  -*-sh-*-
#
# Demonstration and test of a ARTS clear sky simulation with star (sun) 
# including gas scattering and no refraction.
#
# Observation from the ground with one looking direction in the sun 
# and one looking direction 45 deg off the sun.



Arts2 {

INCLUDE "general/general.arts"
INCLUDE "general/continua.arts"
INCLUDE "general/agendas.arts"
INCLUDE "general/planet_earth.arts"

# Agenda for scalar gas absorption calculation
Copy(abs_xsec_agenda, abs_xsec_agenda__noCIA)

# sensor-only path
Copy( ppath_agenda, ppath_agenda__FollowSensorLosPath )

# no refraction
Copy( ppath_step_agenda, ppath_step_agenda__GeometricPath )

# on-the-fly absorption
Copy( propmat_clearsky_agenda, propmat_clearsky_agenda__OnTheFly )

# Clear sky calculation
Copy( iy_main_agenda, iy_main_agenda__Clearsky )

# In coming background radiation
Copy( iy_space_agenda, iy_space_agenda__CosmicBackground)

# standard surface agenda (i.e., make use of surface_rtprop_agenda)
Copy( iy_surface_agenda, iy_surface_agenda__UseSurfaceRtprop)

# specular reflecting surfaces (no polarization)
Copy( surface_rtprop_agenda,surface_rtprop_agenda__Specular_NoPol_ReflFix_SurfTFromt_field)


#gas scattering agenda
AgendaSet( gas_scattering_agenda){
  Ignore(rtp_vmr)
  Ignore(in_los)
  Ignore(out_los)
  
  # propmat_clearskyZero( sca_xsec, f_grid, stokes_dim )
  gas_scatteringCoefXsecConst(ConstXsec=1e-31)  
  gas_scatteringMatrixIsotropic
  
}

# Number of Stokes components to be computed
IndexSet( stokes_dim, 1 )


# Reference ellipsoid
refellipsoidEarth( refellipsoid, "Sphere" )

# Frequency grid 
VectorSet( f_grid, [6e14] )

# A pressure grid rougly matching 0 to 80 km, in steps of 2 km.
VectorNLogSpace( p_grid, 41, 1013e2, 1 )


# Atmospheric dimensionality and lat/lon grids
VectorNLinSpace( lat_grid, 3, -90, 90 )
VectorNLinSpace( lon_grid, 5, -180, 180 )
AtmosphereSet3D

# Set ground altitude z_surface 
MatrixSetConstant( z_surface, 3, 5, 0 )


# Definition of species
abs_speciesSet( species=
            ["H2O", 
             "N2",
             "O2"] )      

# No line data needed here
abs_lines_per_speciesSetEmpty


# Atmospheric profiles
AtmRawRead( basename = "testdata/tropical" )
AtmFieldsCalcExpand1D



# set star source
Touch(stars)
starsAddSingleBlackbody(distance=1.495978707e11, latitude=0., longitude=0.)

# No jacobian calculations
jacobianOff

# No scattering
cloudboxOff

# No sensor
sensorOff

# Check model atmosphere
atmfields_checkedCalc
atmgeom_checkedCalc
cloudbox_checkedCalc

# Propagation path agendas and variables
NumericSet( ppath_lmax, -1 )
NumericSet( ppath_lraytrace, 5e3 )


# Postion and line-of-sight of sensor
MatrixSet( sensor_pos, [ 0, 0, 0; 0, 0, 0 ] )
MatrixSet( sensor_los, [ 0, 0; 45, 0] )


#Set surface relectivity
VectorSet(surface_scalar_reflectivity,[0])

#Switch on gas scattering
IndexSet(gas_scattering_do,1)

#Switch off star
IndexSet(star_do,1)

sensor_checkedCalc
abs_xsec_agenda_checkedCalc
propmat_clearsky_agenda_checkedCalc
lbl_checkedCalc

# Perform RT calculation
yCalc

# To save calculated spectral radiance
#
#output_file_formatSetAscii
# WriteXML( output_file_format, y, "yREFERENCE_ClearSky_StarGasScattering.xml" )


# Check that results are OK with respect to an older reference calculation
#
VectorCreate( y0 )
#
ReadXML( y0, "yREFERENCE_ClearSky_StarGasScattering.xml" )
Compare( y, y0, 0.0001 )


}
 
